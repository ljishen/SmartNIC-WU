#!/usr/bin/env -S gnuplot --persist -c
#
# This script box plots the result generated by script
# summarize_all_platforms.sh in the format of SVG.
#
# Prerequisites:
#   GNU Coreutils >= 8.30
#     env supports the -S/--split-string=S option to split a single
#     argument string into multiple arguments.
#     https://github.com/coreutils/coreutils/blob/master/NEWS
#   gnuplot >= 5.2
#     1. support passing parameters to a gnuplot script (ie. ARGC)
#     http://www.gnuplot.info/ReleaseNotes_5_0_7.html
#
#     2. GPVAL_SYSTEM_ERRNO
#     http://www.gnuplot.info/docs_5.2/Gnuplot_5.2.pdf, p201

if (ARGC != 1) {
  print sprintf("Usage:    %s DATAFILE\n",ARG0)
  print \
    "DATAFILE:\n", \
    "    a datafile summarizes z-score per stressor class for all\n", \
    "    platforms, typically ending with '.platforms_summary.per_class'."
  exit
}

datafile = ARG1
file_ext = system(sprintf("echo '%s' | rev | cut -d'.' -f1-2 | rev", datafile))
if (file_ext ne "platforms_summary.per_class") {
  printerr sprintf("[ERROR] Unsupport datafile: %s", datafile)
  exit status 1
}

system("test -f ".datafile)
if (GPVAL_SYSTEM_ERRNO != 0) {
  printerr sprintf("[ERROR] Cannot find datafile: %s", datafile)
  exit status GPVAL_SYSTEM_ERRNO
}


output_filepath = datafile.".svg"
set output output_filepath

# Mark the first row to be the column header
#   https://stackoverflow.com/a/35528422
set key autotitle columnheader
set key bmargin center horizontal Left noenhanced reverse height 3 font ",11"

data_header = system("grep --max-count=1 '^[^#]' ".datafile)
NF = words(data_header)

set terminal svg enhanced font "arial,12" fontscale 1.0 size (NF-1)*12*10,500

set border 3 front linetype black linewidth 1.000 dashtype solid
set boxwidth 0.9 absolute
set style fill solid 1.00 noborder
set style data histogram
set style histogram clustered gap 5

set xtics border in scale 0,0 nomirror norotate autojustify noenhanced
set ytics border in scale 0,0 nomirror norotate autojustify
set grid ytics

set label font ",15"
set xlabel "stressor class"
set ylabel "accumulated z-score"

plot for [i=2:NF] datafile using i:xtic(1)

print "Written to output file: ".output_filepath
